plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    // id 'signing'
    id 'groovy'
    id 'org.unbroken-dome.test-sets' version '2.2.1'
}


group = 'com.gpaglia.scalatest'
description = 'gradle-scalatest is a Gradle plugin for executing scalatest tests'
version = '0.1.2'
ext {
    websiteUrl = 'https://github.com/gpaglia/gradle-scalatest'
    sourceUrl = 'https://github.com/gpaglia/gradle-scalatest.git'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenLocal()
    mavenCentral()
}

testSets {
    libraries { testCommon }
    unitTest { imports libraries.testCommon }
    integrationTest { imports libraries.testCommon }
    functionalTest {
        imports libraries.testCommon
        extendsFrom integrationTest
    }
}

// Make all tests use JUnit 5
tasks.withType(Test) {
    useJUnitPlatform()
}

dependencies {
    implementation localGroovy()

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testRuntimeOnly files('src/test/lib/testutils.jar')
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:3.2.4'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.2.4'

    testCommonImplementation localGroovy()
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    from "$buildDir/docs/groovydoc"
    archiveClassifier.set('groovydoc')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

test.dependsOn(jar)

artifacts {
    archives groovydocJar
    archives sourcesJar
}

/*
signing {
    sign configurations.archives
}
*/

if (project.version == 'unspecified') {
    version = '0.1-SNAPSHOT'
    // project.signArchives.enabled = false
}

task showVersion() { doLast { println(project.version) } }

gradlePlugin {
    testSourceSets sourceSets.functionalTest
    plugins {
        scalaTest {
            id = 'com.gpaglia.scalatest.gradle-scalatest'
            implementationClass = 'com.gpaglia.scalatest.ScalaTestPlugin'
            description = project.description
        }
    }
}

publishing {

    repositories {
        mavenLocal()
    }
}

